import org.groovymc.modsdotgroovy.gradle.tasks.GatherNeoForgePlatformDetails

plugins {
    // Apply the plugin. You can find the latest version at https://github.com/neoforged/ModDevGradle/packages/2159800.
    id 'net.neoforged.moddev' version '0.1.80'
    id 'org.groovymc.modsdotgroovy' version '2.0.0-beta.28'
}

sourceSets {
    api
}

final mcVersion = "1.${neo_version.split('\\.', 3)[0]}.${neo_version.split('\\.', 3)[1]}"

base {
    archivesName = modid + '-neoforge-' + mcVersion
}

modsDotGroovy {
    platform org.groovymc.modsdotgroovy.types.core.Platform.NEOFORGE
}

tasks.withType(GatherNeoForgePlatformDetails) {
    platformVersion = neoForge.version
    minecraftVersion = mcVersion
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)
neoForge {
    addModdingDependenciesTo(sourceSets.api)

    // We currently only support NeoForge versions later than 21.0.x
    version = "${neo_version}"

    parchment {
        minecraftVersion = '1.20.6'
        mappingsVersion = '2024.06.02'
    }

    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }

    mods {
        okzoomer {
            sourceSet sourceSets.main
            sourceSet sourceSets.api
        }
    }
}

dependencies {
    implementation sourceSets.api.output
    compileOnly group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
    annotationProcessor group: 'com.google.auto.service', name: 'auto-service', version: '1.0.1'
}
